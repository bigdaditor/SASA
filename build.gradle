plugins {
    id 'java-library'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'maven-publish'
    id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'SASA - Spring API Spec Analyzer Library'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:4.0.1"
    }
}

dependencies {
    // Spring MVC 의존성 (Spring Boot 없이)
    api 'org.springframework:spring-webmvc'
    api 'org.springframework:spring-context'

    // Jackson
    api 'com.fasterxml.jackson.core:jackson-databind'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // Validation API (optional dependency for validation annotations)
    compileOnly 'jakarta.validation:jakarta.validation-api'

    // Validation API for test (runtime dependency)
    testImplementation 'org.springframework.boot:spring-boot-starter-validation'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework:spring-test'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}

// Custom task to run test Spring Boot application
task runTestApp(type: JavaExec) {
    group = 'application'
    description = 'Runs the test Spring Boot application to generate API spec'
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'com.example.sasa.TestApplication'
}

// Automatically publish to Maven Local after build
tasks.named('build') {
    finalizedBy 'publishToMavenLocal'
}

// Library publishing configuration
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'SASA'
                description = 'Spring API Spec Analyzer - Runtime API specification extraction library'
                url = 'https://github.com/bigdiaditor/sasa'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }

            // Resolved dependencies 발행
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}
